buildscript {
    ext {
        springBootVersion = '2.6.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}

plugins {
    id 'java'
}

// 하위 모든 프로젝트 공통 세팅
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'com.example'
    version '1.0-SNAPSHOT'

    sourceCompatibility = '11'
    targetCompatibility = '11'
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    // 하위 모듈에서 공통으로 사용하는 세팅 추가
    dependencies {

        implementation 'org.springframework.boot:spring-boot-starter-web'

        compileOnly 'org.projectlombok:lombok'
        runtimeOnly 'com.h2database:h2'
        testCompileOnly 'org.projectlombok:lombok'

        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    }
}

project(':core') {
    // 공통 코드
    bootJar { enabled = false } // 부팅 x
    jar { enabled = true } // jar o
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-validation' // 유효성 검사 라이브러리 추가
        implementation 'org.springframework.boot:spring-boot-starter-cache' // 캐시 기능 추가
        implementation 'com.github.ben-manes.caffeine:caffeine:3.1.1' // 캐시 기능 추가
    }
}

project(':api') {

    bootJar {
        enabled = true
        archivesBaseName = 'kakaobank'
        archiveFileName = 'kakaobank.jar'
    } // 부팅 o

    jar {
        enabled = false // jar x
        manifest {
            attributes 'Main-Class': 'com.example.ApiApplication'
        }
    }

    dependencies {
        // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.0' // string blank 검사
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-jdbc'
        implementation project(':core')
        implementation project(':external-api')
    }

    test {
        useJUnitPlatform()
    }
}

project(':external-api') {
    bootJar { enabled = false } // 부팅 x
    jar { enabled = true } // jar o

    dependencies {
        implementation project(':core')
    }

}
