buildscript {
    ext {
        springBootVersion = '2.6.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}

plugins {
    id 'java'
}
// 하위 모든 프로젝트 공통 세팅
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'org.example'
    version '1.0-SNAPSHOT'

    sourceCompatibility = '11'
    targetCompatibility = '11'
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    // 하위 모듈에서 공통으로 사용하는 세팅 추가
    dependencies {

        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation' // 유효성 검사 라이브러리 추가

        compileOnly 'org.projectlombok:lombok'
        runtimeOnly 'com.h2database:h2'
        testCompileOnly 'org.projectlombok:lombok'

        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

    }
}

project(':core') {
    // 공통 코드
    bootJar { enabled = false } // 부팅 x
    jar { enabled = true } // jar o

}

project(':api') {
    bootJar { enabled = true } // 부팅 o
    jar { enabled = false } // jar x
    dependencies {
        implementation project(':core')
        implementation project(':external-api')
    }


}

project(':external-api') {
    bootJar { enabled = false } // 부팅 x
    jar { enabled = true } // jar o

    dependencies {
        implementation project(':core')
    }

}

project(':cache') {
    bootJar { enabled = false } // 부팅 x
    jar { enabled = true } // jar o

    dependencies {
        implementation project(':core')
    }

}